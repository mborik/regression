		device	zxspectrum128

numberOfNeedles = 5
		include "pg1fx.inc"
		include "pg3fx.inc"
		include "pg4fx.inc"
		include "pg6fx.inc"
		include "pg7fx.inc"


page128		equ	#7FFD
act128page	equ	#5B5C
actborder	equ	#5C48
FRAMES		equ	23672
mb02ram		equ	#17


		org	#8000

@start:		di
		ld	sp,tempsp
		xor	a
		out	(254),a
		ld	(actborder),a
		ld	a,#10
		call	xchg.bnk
restart:	ld	hl,screen.lzx
		ld	de,#4000
		call	depack

		ld	a,#61
		out	(mb02ram),a

loader:		ld	e,0	; (0 = root)
		ld	a,#14	; SETDIR
		rst	#20

		ld	hl,fn.haystack
		ld	e,#03
		ld	a,#19	; SEASX
		rst	#20
		jp	nz,error ; not found!

		ld	a,#12	; GETCST
		rst	#20
		ld	(.fcursor+1),hl
		ld	(needle.fcursor+1),hl

.fcursor:	ld	hl,0
.fpos:		ld	bc,0
		ld	ix,#C000
		ld	e,16
		ld	a,c
		add	a,e
		ld	(.fpos+1),a
		ld	a,#23	; LOA2X
		rst	#20

.blktabptr:	ld	hl,blocktable
.targetpg:	ld	bc,(blocktable.firstct << 8) | #64 ; SRAM page 4 (WR)
.depk6:		ld	e,(hl)
		inc	hl
		ld	d,(hl)
		inc	hl
		push	hl
		ex	hl,de
		ld	de,0
		ld	a,c
		inc	c
		push	bc
		out	(mb02ram),a	; switch bank
		call	depack

.prgptr:	ld	hl,#55A1
		ld	(hl),#FF	; draw progress
		inc	l
		ld	(.prgptr+1),hl

		pop	bc
		pop	hl
		djnz	.depk6
		ld	(.blktabptr+1),hl

.countptr:	ld	hl,blocktable.counts
		ld	b,(hl)
		inc	hl
		ld	(.countptr+1),hl
		xor	a
		or	b
		jr	z,.fullfilprg-3
		ld	(.targetpg+1),bc
		ld	a,#61
		out	(mb02ram),a
		jr	.fcursor

		ld	hl,(.prgptr+1)
.fullfilprg:	ld	a,l
		cp	#BF
		jr	z,needle
		ld	(hl),#FF
		inc	l
		jr	.fullfilprg

needle:		ld	a,#61
		out	(mb02ram),a
		ld	a,numberOfNeedles
.loop:		push	af
.fcursor:	ld	bc,0
		inc	bc
		ld	(.fcursor+1),bc
		push	bc
		ld	ix,direntrybuf
		ld	a,#16	; GETSUB
		rst	#20
		jr	z,error

		ld	a,(ix+0)
		cp	#B0
		jr	nz,.invalidfile
		ld	xl,6
		ld	de,fn.needle
.fncompare:	ld	a,(de)
		or	a
		jr	z,.rightname
		cp	(ix+0)
		jr	nz,.invalidfile
		inc	e
		inc	xl
		jr	.fncompare

.invalidfile:	pop	af
		pop	af
		jr	.loop

.rightname:	ld	a,(ix+0)
		sub	('0' - #10)
		ld	(.targetpg+1),a
		ld	a,(ix+1)
		sub	' '
		jr	nz,error
.targetpg:	ld	a,0
		call	xchg.bnk
		ld	e,16
		ld	bc,0
		pop	hl
		ld	ix,#C000
		ld	a,#23	; LOA2X
		rst	#20

.prgptr:	ld	hl,#56A1 ; #5AA1
		ld	bc,((30 / numberOfNeedles) << 8) | #FF
.pgprog:	ld	(hl),c	; draw 2nd progress
		inc	l
		djnz	.pgprog
		ld	(.prgptr+1),hl

		pop	af
		dec	a
		jr	nz,.loop
		ld	b,25
.fadeout:	ei
		halt
		halt
		push	bc
		call	attrhide
		pop	bc
		djnz	.fadeout
		jp	kernel

error:		ld	hl,txterr
		ld	a,#34	;; MESSX
		rst	#20
		jp	c,restart
		rst	0

xchg.vram:	ld	a,#1D
		xor	#0A
		ld	(xchg.vram+1),a
xchg.bnk:	ld	(act128page),a
		ld	bc,page128
		out	(c),a
		ret


	if $ > #8128
		display "Too long! ",/D,($-#8128)," bytes out!"
	else
		display /D,(#8128-$)," bytes free..."
	endif

		org	#8128

;; Setting for depacker according to compression identification -tXYoAoB

com	=	3	;; Compression type - one from TYPZX7 TYPBLK TYPBS1
pos	=	6	;; Offset coding - one from POSOF4 POSOF1 POSOF2 POSOFD
ofs1	=	7	;; Number of bits for 1st offset - required for POSOF1 POSOF2 POSOF4
ofs2	=	14	;; Number of bits for 2nd offset - required for POSOF2 only

;; This is needed to set according to compression ID string from name of compressed file:
;;  -tXYoAoB ... means setting  com=X, pos=Y, ofs1=A, ofs2=B   (ofs1 ofs2 only when required)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Set optimization of depacker

spd	=	2

;; Possible values:
;;  0 ... optimized for code length - short but slow
;;  1 ... compromise between length and speed
;;  2+ .. optimized for speed - but longer code

depack:		include "lzx.a80"

;;- INTERRUPT -----------------------------------------------------------------
;		org	#8181

interrupt:	push	af		; zakladne osetrenie prerusenia
		ld	a,(actborder)	; s volanim hudobneho hradla
		out	(254),a
		push	bc
		push	de
		push	hl
		exx
		ex	af,af'
		push	ix
		push	iy
		push	af
		push	bc
		push	de
		push	hl

@IMCALL		db	0,0,0

		ld	a,(act128page)
		push	af
		and	$F8		; ponechame nastavenu vram
		or	MUSIC.page	; a prestrankujeme na banku s muzikou
		ld	bc,page128
		out	(c),a
		call	MUSIC.play
		pop	af
		ld	bc,page128
		out	(c),a

		ld	hl,(FRAMES)
		inc	hl
		ld	(FRAMES),hl

		pop	hl
		pop	de
		pop	bc
		pop	af
		pop	iy
		pop	ix
		ex	af,af'
		exx
		pop	hl
		pop	de
		pop	bc
		pop	af
		ei
		ret


fxtable:	db	fx_city.pg
		dw	fx_city
		dw	fx_city.sync

		db	fx_reglogo.pg
		dw	fx_reglogo
		dw	fx_reglogo.sync

		db	fx_oneplusani.pg
		dw	fx_oneplusani
		dw	fx_oneplusani.length

		db	fx_oneplus.pg
		dw	fx_oneplus
		dw	fx_oneplus.sync

		db	fx_surprise.pg
		dw	fx_surprise
		dw	fx_surprise.sync

		db	fx_trafoutro.pg
		dw	fx_trafoutro
		dw	fx_trafoutro.length

		db	fx_transformy.pg
		dw	fx_transformy
		dw	fx_transformy.sync

		db	fx_greetings.pg
		dw	fx_greetings
		dw	fx_greetings.sync

		db	fx_turndisk.pg
		dw	fx_turndisk
		dw	fx_turndisk.length

		db	fx_ripple.pg
		dw	fx_ripple
		dw	fx_ripple.sync

		db	fx_finalpart.pg
		dw	fx_finalpart
		dw	fx_finalpart.sync

		db	0

;;-----------------------------------------------------------------------------
		align	256

im2vector:
direntrybuf:	ds	256 - (attrhide.end - txterr)

@tempsp
txterr		db	"DEMO-KERNEL LOADING FAILED!"
		dw	#C900

fn.needle:	db	"needle",0
fn.haystack:	db	"haystack  "
blocktable:	include "../haystack.inc"


attrhide:	ld	hl,22528
		ld	c,24
.atthd2:	ld	b,32
		push	bc
.atthd3:	ld	a,(hl)
		and	7
		jr	z,.atthd4
		dec	a
.atthd4:	push	af
		ld	a,(hl)
		and	56
		jr	z,.atthd5
		ld	c,8
		sub	c
		ld	c,a
		pop	af
		or	c
		jr	.atthd6
.atthd5:	ld	a,(hl)
		and	248
		ld	c,a
		pop	af
		or	c
.atthd6:	ld	(hl),a
		inc	hl
		djnz	.atthd3
		pop	bc
		dec	c
		jr	nz,.atthd2
		ex	af,af'
		ret
.end = $

;;-----------------------------------------------------------------------------
		align	256

		nop
kernel:		di
		ld	sp,basesp
		ld	hl,im2vector
		ld	a,h
		ld	i,a
		im	2
		ld	a,high interrupt
.mkim2:		ld	(hl),a
		inc	l
		jr	nz,.mkim2
		inc	h
		ld	(hl),a
; init music
		ld	a,MUSIC.page | #10
		call	xchg.bnk
		call	MUSIC.init
; clear both vrams
		ld	a,#17
		call	xchg.bnk
		ld	hl,#4000
		ld	de,#4001
		ld	bc,6911
		ld	(hl),l
		ldir
		ld	hl,#4000
		ld	d,#C0
		ld	b,high 6912
		ldir
		ld	(FRAMES),bc
		ld	a,#10
		call	xchg.bnk

.run:		ld	hl,fxtable
		ld	a,(hl)
		or	a
		jr	z,.end
		inc	l
		ld	e,(hl)
		inc	l
		ld	d,(hl)
		push	hl
		ex	de,hl
		ld	e,a
		and	#17
		ld	b,a
		ld	a,(act128page)
		and	#F8
		push	af
		or	b
		ld	bc,page128
		ld	(act128page),a
		out	(c),a
		bit	7,e
		jr	nz,.ldir
		push	bc
		ld	de,RUNZONE
		call	depack
		pop	bc
		pop	af
		ld	(act128page),a
		out	(c),a
		pop	hl
		inc	l
		ld	e,(hl)
		inc	l
		ld	d,(hl)
		inc	l
		ld	(.run+1),hl
		call	RUNZONE
		jr	.run

.ldir:		pop	af
		ex	de,hl
		pop	hl
		inc	l
		ld	c,(hl)
		inc	l
		ld	b,(hl)
		inc	l
		ld	(.run+1),hl
		ex	de,hl
		ld	de,RUNZONE
		ldir
		ld	bc,page128
		ld	(act128page),a
		out	(c),a
		call	RUNZONE
		jr	.run

.end		ei
		halt
		jr	.end
@kernend

		align	256
@RUNZONE	equ	$
basesp

		org	kernend

screen.lzx:	incbin "loading.lzx"


		emptytap "REGRESSION.tap"
		savetap  "REGRESSION.tap",CODE,"REGRESSION",start,$-start


		export page128
		export act128page
		export actborder
		export FRAMES
		export mb02ram
		export MUSIC.page
		export MUSIC.init
		export MUSIC.play
		export IMCALL
		export RUNZONE
		export depack
		export xchg.vram
		export xchg.bnk
